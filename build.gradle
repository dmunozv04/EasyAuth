plugins {
    id "fabric-loom" version "0.11-SNAPSHOT"
}

repositories {
    maven { name "Carpet"; url "https://masa.dy.fi/maven" }
    maven { name "Translation"; url "https://maven.nucleoid.xyz/" }
    maven { name "Jitpack"; url "https://jitpack.io" }
    maven { name "Permission"; url "https://oss.sonatype.org/content/repositories/snapshots"}
    mavenLocal()
}

archivesBaseName = "${project.mod_id}-mc${project.minecraft_version}"
version = "${project.mod_version}"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

loom {
    accessWidenerPath = file("src/main/resources/easyauth.accesswidener")
}

dependencies {
    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Translations
    // modImplementation(include("fr.catcore:server-translations-api:${project.server_translations_version}+${project.server_translations_branch}"))

    // Permissions
    modImplementation(include("me.lucko:fabric-permissions-api:${project.fabric_permissions_version}"))

    // Mods
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "carpet:fabric-carpet:${project.carpet_branch}-${project.carpet_core_version}"
    modImplementation "com.github.astei:lazydfu:${lazydfu_version}"

    // Password hashing
    // Argon2
    implementation(include("de.mkammerer:argon2-jvm:${argon2_version}"))
    implementation(include("de.mkammerer:argon2-jvm-nolibs:${argon2_version}"))

    // BCrypt
    implementation(include("at.favre.lib:bcrypt:${bcrypt_version}"))
    implementation(include("at.favre.lib:bytes:${bytes_version}"))

    // Storage
    // LevelDB database
    implementation(include("org.iq80.leveldb:leveldb:${leveldb_version}"))
    implementation(include("org.iq80.leveldb:leveldb-api:${leveldb_version}"))

    // MongoDB driver
    implementation(include("org.mongodb:mongodb-driver-sync:${mongodb_version}"))
    implementation(include("org.mongodb:mongodb-driver-core:${mongodb_version}"))
    implementation(include("org.mongodb:bson:${mongodb_version}"))

    // MySQL driver
    implementation(include("mysql:mysql-connector-java:${mysql_version}"))

    // JNA lib
    include "net.java.dev.jna:jna:${jna_version}"

    // Placeholder API
    // modImplementation include("eu.pb4:placeholder-api:1.1.3+1.17.1")
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from "LICENCE"
}

processResources {
    inputs.property "id", project.mod_id
    inputs.property "name", project.mod_name
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand(["id": project.mod_id, "name": project.mod_name, "version": project.version])
    }
}

